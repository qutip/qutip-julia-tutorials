---
title: "The Dicke Model"
author: Li-Xun Cai
date: 2025-03-02  # last update (keep this comment as a reminder)

engine: julia
---

Inspirations taken from [this QuTiP tutorial](https://nbviewer.org/urls/qutip.org/qutip-tutorials/tutorials-v5/lectures/Lecture-3A-Dicke-model.ipynb) by J. R. Johansson.

This tutorial mainly demonstrates the use of 

- [`jmat`](https://qutip.org/QuantumToolbox.jl/stable/resources/api#QuantumToolbox.jmat)
- [`steadystate`](https://qutip.org/QuantumToolbox.jl/stable/resources/api#QuantumToolbox.steadystate)
- [`wigner`](https://qutip.org/QuantumToolbox.jl/stable/resources/api#QuantumToolbox.wigner)
- [`entropy_mutual`](https://qutip.org/QuantumToolbox.jl/stable/resources/api#QuantumToolbox.entropy_mutual)

by exploring the Dicke model.

## Introduction

The Dicke model describes a system where $N$ two-level atoms interact with a single quantized electromagnetic mode. The original microscopic form of the Dicke Hamiltonian is given by:

$$
H_D = \omega a^\dagger a + \sum_{i=1}^{N} \frac{\omega_0}{2} \sigma_z^{(i)} + \sum_{i=1}^{N} \frac{g}{\sqrt{N}} (a + a^\dagger) (\sigma_+^{(i)} + \sigma_-^{(i)})
$$

where:

- $a$, $a^\dagger$ are the annihilation and creation operators for the cavity mode, with frequency $\omega$.
- $\sigma_z^{(i)}, \sigma_\pm^{(i)}$ are the Pauli matrices for the $i$-th two-level atom, with transition frequency $\omega_0$.
- $g$ represents the light-matter coupling strength.

This formulation explicitly treats each spin individually. However, when the atoms interact identically with the cavity, we can rewrite the Hamiltonian regarding collective spin operators.

$$
J_z = \sum_{i=1}^{N} \sigma_z^{(i)}, \quad J_{\pm} = \sum_{i=1}^{N} \sigma_{\pm}^{(i)}, 
$$
which describe the total spin of the system as a pseudospin of length $j = N/2$. Using these collective operators, the reformulated Dicke Hamiltonian takes the form:

$$
H_D = \omega a^\dagger a + \omega_0 J_z + \frac{g}{\sqrt{N}} (a + a^\dagger) (J_+ + J_-)
$$

This formulation reduces complexity, as it allows us to work on a collective basis instead of the whole individual spin Hilbert space. The superradiant phase transition occurs when the coupling strength $g$ exceeds a critical threshold, leading to a macroscopic population of the cavity mode.


## Code demonstration

```{julia}
using QuantumToolbox
using CairoMakie
```

```{julia}
ω = 1
ω0 = 1

gc = √(ω * ω0) / 2

κ = 0.05;
```

Here, we define some functions for later usage.
```{julia}
# M: cavity Hilbert space truncation, N: number of atoms
Jz(M, N) = (qeye(M) ⊗ jmat(N/2, :z))

a(M, N) = (destroy(M) ⊗ qeye(N+1))

function H(M, N, g)
 j = N / 2
 n = 2 * j + 1
    
 a_ = a(M, N)
 Jz_ = Jz(M, N)
 Jp = qeye(M) ⊗ jmat(j, :+)
 Jm = qeye(M) ⊗ jmat(j, :-);
    
 H0 = ω * a_' * a_ + ω0 * Jz_
 H1 = 1/ √N * (a_ + a_') * (Jp + Jm)

    return (H0 + g * H1)
end;
```

Take the example of 4 atoms.
```{julia}
M0, N0 = 16, 4

H0(g) = H(M0, N0, g)

a0 = a(M0, N0)
Jz0 = Jz(M0, N0);
```

```{julia}
gs = 0.0:0.05:1.0
ψGs = Vector{QuantumObject{KetQuantumObject}}()
for _g in gs
 _H = H0(_g)
 _, _ψ = eigenstates(_H)
    push!(ψGs, _ψ[1])
end

nvec = expect(a0'*a0, ψGs)
Jzvec = expect(Jz0, ψGs);
```

```{julia}
fig = Figure(size = (800, 300))
axn = Axis(
 fig[1,1],
 xlabel = "interaction strength",
 ylabel = L"\langle \hat{n} \rangle"
)
axJz = Axis(
 fig[1,2],
 xlabel = "interaction strength",
 ylabel = L"\langle \hat{J}_{z} \rangle"
)
ylims!(-N0/2, N0/2)
lines!(axn, gs, real(nvec))
lines!(axJz, gs, real(Jzvec))
display(fig);
```
The expectation value of photon number and $J_z$ showed a sudden increment around $g_c$. 

```{julia}
queue = [1,6,11,16,20]
fig = Figure(size = (1050,600))

ax_bot = Axis(fig[3,1:6], height=200)
ax_bot.title=L"\langle \hat{n} \rangle"
xlims!(ax_bot, 0, maximum(gs))
lines!(ax_bot, gs, real(nvec), color=:teal)
lines!(ax_bot, [gc, gc], [0,5])

for (hpos, idx) in enumerate(queue)
 _g = gs[idx]
    lines!(ax_bot, [_g,_g], [0,5], linestyle=:dash, linewidth=4, color=:blue)
 _ax = Axis(
 fig[1,hpos],
 aspect = 1,
 title="g = $_g"
 )
 _ρcav = ptrace(ψGs[idx], 1)
 _wig = wigner(_ρcav, -7:0.07:7, -7:0.07:7)
 _hm = heatmap!(_ax, -7:0.07:7, -7:0.07:7, _wig', colorrange=(0,0.35))
 (hpos == 5) && (Colorbar(fig[1,6], _hm))
    
 _ρcav = ptrace(ψGs[idx], 1)
    barplot(fig[2,hpos], real(diag(_ρcav)), label=L"g = $_g")
end

lines!(ax_bot, [gc, gc], [0,5])

display(fig);
```
As $g$ increases, the cavity ground state's wigner function plot looks more coherent than a thermal state.

```{julia}
slists = []
Ns = 8:8:24

for N in Ns
 slist = []
    for g in gs
 H_ = H(M0, N, g)
 _, vecs = eigenstates(H_)
 S = entropy_mutual(vecs[1], 1, 2)
        push!(slist, S)
    end

    push!(slists, slist)
end
```

```{julia}
fig = Figure()
ax = Axis(fig[1,1])
ax.xlabel = "coupling strength"
ax.ylabel = "mutual entropy"

for (idx, slist) in enumerate(slists)
    lines!(gs, slist, label = string(Ns[idx]))
end

Legend(fig[1,2], ax, label = "number of atoms")
display(fig);
```
We further consult mutual entropy between the cavity and the spins as a measure of their correlation; the result showed that as the number of atoms $N$ increases, the peak of mutual entropy moves closer to $g_c$.



## Version Information
```{julia}
QuantumToolbox.versioninfo()
```
